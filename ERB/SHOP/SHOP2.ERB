;大人数対応キャラ소환処理
;あと플래그1000番台を使うので注意
;TFLAG:0～199は조교関係ない上, 조교개시時にリセットされるので無問題

;変更点
;소환したキャラが直接加入するように, 소환処理のあたりを変更
;@CHARA_LISTを消して@CHARA_BUYと@CHARA_BUY_SHOWに吸収. 
;同じものを表している変数は同じにした. (予期せぬ所で変な数が入ることはないはず・逆にややこしいかも…)
;買えるキャラが一人もいなくなった時にRETURN. 
;캐릭터의아이템番号以外の数字を入力したときの処理をRETURNからGOTO INPUT_LOOPに. 
;@CHARA_BUY_AFTERでリセットされていなかったTFLAGを含めて, ITEMSALESとTFLAGのリセットをまとめた. 
;ここ変更したら@START_CHARA_SELECTの方にも目を通すの必須なんだ、すまない

@CHARA_BUY
#DIM MEMO_LINECOUNT

REDRAW 0
;キャラメイク中フラグON
SETBIT FLAG:99, 13

MEMO_LINECOUNT = LINECOUNT

PRINTL 
CALL CHARA_BUY_SHOW

$INPUT_LOOP
INPUT

IF RESULT == 100
	RETURN 0
;数値は캐릭터의아이템番号の最小値と최대치依存。
ELSEIF RESULT < 0 || RESULT > 98
	GOTO INPUT_LOOP
;売値の설정がないなら再入力
ELSEIF ITEMSALES:(RESULT + 100) == 0
	GOTO INPUT_LOOP
;５人이상攻略しないと스투나・멜로나を소환できない([수수께끼의매력]が필요)
ELSEIF (RESULT == 34 || RESULT == 35) && TALENT:MASTER:수수께끼의매력 == 0
	PRINTL 조교가능조건을 만족하지 못했습니다
	GOTO INPUT_LOOP
;플래그値＝売値が소지금より多いなら再入力
ELSEIF MONEY < ITEMPRICE:(RESULT + 100)
	PRINTL 소지금이 부족합니다
	GOTO INPUT_LOOP
ENDIF

;C = 캐릭터번호, E = 上位種체크(@CHARA_MANUAL)
C = RESULT
;最初のページは基本種
E = 0
;キャラ解説を入れる
CALL CHARA_MANUAL, C
;いいえを選ぶと再入力
IF RESULT == 0
	;表示されていたキャラを하양紙に戻す
	DELCHARA CHARANUM-1

	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	RESTART
ENDIF

;基本種を選んだならE=0
SIF E < 2
	E = 0

;上記処理を抜けてきた＝소환가능で소지금が売値이상
MONEY -= ITEMPRICE:(100 + C)
TARGET = CHARANUM - 1

;유니크キャラは소환사고がおきない
IF IS_SAMONTRBL()
	SIF FLAG:8
		PRINTL ★★★소환사고가 발생 !! ★★★
	CALL CHAR_SAMON_T, C
ENDIF

PRINTFORMW ★%조사처리(NAME:TARGET,"를")% 소환했습니다★

RETURN 0

@IS_SAMONTRBL()
#FUNCTION
;[666]で소환or소환천재（天災）
SIF FLAG:666 || TALENT:MASTER:소환천재（天災）
	RETURNF 1
;소환천재（天才）＋事故あり
SIF TALENT:MASTER:소환천재（天才） && CONFIG("소환사고")
	RETURNF 1
;유니크캐릭터의時には컨피그で설정必須
SIF COND("유니크") && CONFIG("소환사고：유니크") == 0
	RETURNF 0
SIF CONFIG("소환사고") && RAND:3 == 0
	RETURNF 1
RETURNF 0

@CHARA_BUY_SHOW
DRAWLINE
PRINTFORM {DAY}일째
IF TIME == 0
	PRINTL  낮
ELSE
	PRINTL  밤
ENDIF
SIF TARGET
	PRINTFORM %NAME:TARGET% 조교中
SIF ASSI
	PRINTFORM （%TEXTS("조수の명칭")%：%NAME:ASSI%）
PRINTFORML (잔액 %MONEY_C(MONEY)%)
CALL PRINT_STR, "하트핑크_H_엔딩이 있는 캐릭터는 색이 바뀝니다_하트핑크_H_L_"
DRAWLINE

CALL SET_CHARA_BUY_SHOW, "ＥＮＤ체크"

DRAWLINE
PRINTFORML [100] 소환하지 않는다
PRINTFORML  소환하고 싶은 캐릭터의 번호를 입력해 주세요


;-------------------------------------------------
;매각キャラorMASTERとして선택する캐릭터의일람表示
;ARGSに"ＥＮＤ체크"って入れると엔딩が書かれている캐릭터의色が変わる
;-------------------------------------------------
@SET_CHARA_BUY_SHOW, ARGS
#DIM LCOUNT
#DIM LCOUNT2
#DIM NEXT_LINE

VARSET ITEMSALES

;バグ対策. 表示されるキャラ数が一ページの表示限界数(この場合は30)より少ないとITEMSALES:0が登録されるので. 
ITEMSALES:0 = 0

NEXT_LINE = 0
;実際にキャラを表示(80番이상は비표시)
FOR LCOUNT, 1, 80
	;既に유니크キャラを購入済みなら買えなくする
	SIF FLAG:(1000 + LCOUNT) > 0 && CONFIG("유니크캐릭터의다중소환") == 0
		CONTINUE
	;価格があるか判定. ITEMPRICEの有無で判断する. 
	SIF ITEMPRICE:(100 + LCOUNT) == 0
		CONTINUE

	ITEMSALES:(100 + LCOUNT) = 1

	IF ARGS == "ＥＮＤ체크"
		FOR LCOUNT2, 1, 10
			RESULT = 0
			TRYCALLFORM IS_SINGLE_ENDING_K{LCOUNT}_{LCOUNT2}
			SIF RESULT
				SETCOLOR DEF_COLOR("하트핑크")
		NEXT
	ENDIF

	PRINTFORMLC [{LCOUNT, 2}] %TEXT_LJ(ITEMNAME:(100 + LCOUNT), 16)% %TEXT_RJ(MONEY_C(ITEMPRICE:(100 + LCOUNT) ), 7)% 

	RESETCOLOR

	NEXT_LINE += 1
	SIF NEXT_LINE%2 == 0
		PRINTL 
NEXT
SIF NEXT_LINE%2
	PRINTL 

@SET_NEWNAME, ARG
SIF ARG < 0
	RETURN 0
SIF ARG == 0 && TARGET
	ARG = TARGET

NAME:ARG = %NAMES("종족", NO:ARG, CFLAG:ARG:8)%
SIF CFLAG:ARG:8
	CALLNAME:ARG = %NAME:ARG%

