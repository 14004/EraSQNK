;eraSQ製作スレッド９  738氏 10/09/01 作成 	10/09/02 更新
;-----------------------------------------------------------------------------------------
@ACCESSORY_ORDERMADE_MENU
#DIM LCOUNT
#DIM NUM_ACCE
#DIM MEMO_PRICE

;まずは何個目かを判定
FOR LCOUNT, 1, 9
	IF FLAG:(300 + LCOUNT*10) == 0
		NUM_ACCE = LCOUNT
		BREAK
	ENDIF
NEXT

PRINTL 
PRINTL 「너의 취향에 맞는 악세사리를 만들어주지. 
PRINTL  아무래도 값은 나가겠지만, 분명 마음에 들을거야. 
PRINTL  그럼, 무슨 기반으로 만들어볼까？」
PRINTL （어느쪽을 기반으로 해도 가격은 변하지 않습니다）
PRINTL  [ 0] 브레슬릿 정신력중시 
PRINTL  [ 1] 커프스 매력중시 
PRINTL  [ 2] 건틀렛 기교중시 
PRINTL  [ 3] 새시 정력중시 
PRINTL  [ 4] 반지 밸런스중시
PRINTL [100] 돌아간다

CALL INPUT_SELECT, 5, 100

SIF RESULT == 100
	RETURN 0

FLAG:(300 + NUM_ACCE*10) = RESULT*10

PRINTL 
PRINTL 「좋아, 재료는 뭘로할래?」

SELECTCASE FLAG:(300 + NUM_ACCE*10)
CASE 0
	PRINTL [0]상아 [1]청동 [2]백은 [3]소울실버
	CALL INPUT_SELECT, 4
CASE 10
	PRINTL [0]산호초 [1]포실 [2]제이드 [3]앰버
	CALL INPUT_SELECT, 4
CASE 20
	PRINTL [0]실크 [1]새튼 [2]빌로드
	CALL INPUT_SELECT, 3
CASE 30
	PRINTL [0]숫말 [1]숫사슴 [2]숫토끼
	CALL INPUT_SELECT, 3
CASE 40
	PRINTL [0]크리스탈 [1]다이아몬드
	CALL INPUT_SELECT, 2
ENDSELECT
FLAG:(300 + NUM_ACCE*10) += RESULT + 1

PRINTL 
PRINT 「최대 
CALL PRINT_COLORTEXT, "５つ", DEF_COLOR("황색")
PRINTL 까지 붙일 수 있지만… 어빌리티를 몇개 부여할까？
PRINTL  다만, 붙이는 어빌리티가 많아질수록 대금은 비싸지지. 
PRINT  그렇구만, 
CALL PRINT_COLORTEXT, "１개라면 서비스로 $5,000", DEF_COLOR("황색")
PRINTL 에 해주지. 
PRINT  ２개 이상을 붙인다면…
CALL PRINT_COLORTEXT, "어빌리티 수×$10,000", DEF_COLOR("황색")
PRINTL 정도 대금을 받아야겠어. 
PRINTW  부여하는 수가 많으면, 꽤나 돈이 드니까. 신경쓰라고」

$MAKE_ACCE

CALL SET_ACCESSORY_ABILITY, NUM_ACCE

PRINTL 「그래, 이녀석으로 할까?」

CALL PRINT_ACCESSORY, NUM_ACCE

VARSET LOCAL
FOR LCOUNT, 1, 6
	SIF FLAG:(300 + NUM_ACCE*10 + LCOUNT) == 0
		BREAK
	LOCAL += 1
NEXT

;お代の計算
SELECTCASE LOCAL
CASE 0
	MEMO_PRICE = 0
CASE 1
	MEMO_PRICE = 5000
CASEELSE
	MEMO_PRICE = LOCAL*10000
ENDSELECT

PRINTFORM 「대금은……아까 말한대로, 
CALL PRINT_COLORTEXT, MONEY_C(MEMO_PRICE), DEF_COLOR("황색")
PRINTFORML 인데 문제 없겠어？」
PRINTL 

PRINTFORML  [ 0] 문제없어 （소지금：%MONEY_C(MONEY)% … \@ MONEY >= MEMO_PRICE ? 예산 내 # 예산오버! \@）
PRINTFORML  [ 1] 한번 더 생각한다
PRINTFORML [100] 다시합니다

$ORDER_BUY_ANS
INPUT

IF RESULT == 100
	VARSET FLAG, 0, 300 + NUM_ACCE*10, 310 + NUM_ACCE*10
	PRINTW 「뭐야…놀리는거냐」
ELSEIF RESULT == 0 && MONEY < MEMO_PRICE
	PRINTL 「돈이 부족해…」
	GOTO ORDER_BUY_ANS
ELSEIF RESULT == 0
	PRINTW 「매번 고마워」
	MONEY -= MEMO_PRICE
ELSEIF RESULT == 1
	GOTO MAKE_ACCE
ELSE
	GOTO ORDER_BUY_ANS
ENDIF

@SET_ACCESSORY_ABILITY, ARG
#DIM LCOUNT
#DIM MEMO_PRICE
#DIM MEMO_LINECOUNT
MEMO_LINECOUNT = LINECOUNT

REDRAW 0

PRINTL  
VARSET LOCAL
FOR LCOUNT, 1, 6
	SIF FLAG:(300 + ARG*10 + LCOUNT) == 0
		BREAK
	LOCAL += 1
NEXT

;お代の計算
SELECTCASE LOCAL
CASE 0
	MEMO_PRICE = 0
CASE 1
	MEMO_PRICE = 5000
CASEELSE
	MEMO_PRICE = LOCAL*10000
ENDSELECT
;선택지の存在체크
FOR LCOUNT, 1, 101
	SELECTCASE LCOUNT
	CASE 1 TO 6, 10 TO 14, 20 TO 24, 30 TO 33, 50 TO 52
		SIF LOCAL <= 4
			LOCAL:LCOUNT = 1
	CASE 99, 100
		SIF LOCAL
			LOCAL:LCOUNT = 1
	ENDSELECT
NEXT

SELECTCASE LOCAL
CASE 0
	PRINTFORML 「어느 어빌리티를 붙일까？」
CASEELSE
	PRINTFORML 「일단은 이런 느낌이야」
	CALL PRINT_ACCESSORY, ARG

	PRINT 「대금은 

	CALL PRINT_COLORTEXT, MONEY_C(MEMO_PRICE), DEF_COLOR("황색")

	SELECTCASE LOCAL
	CASE 5
		PRINTFORML 인데,  이이상 붙일 수는 없어」
	CASEELSE
		PRINTFORML 인데, 또 할거야？」
	ENDSELECT
ENDSELECT
PRINTL 

SIF LOCAL >= 5
	SETCOLOR DEF_COLOR("회색")

PRINTFORML  [ 1] %TEXT_RJ("ＥＰ+30%", 14)%　…　사정게이지의 ＭＡＸ가 30％ 증가
PRINTFORML  [ 2] %TEXT_RJ("정력+30%", 14)%　…　정력의 ＭＡＸ가 30％ 증가
PRINTFORML  [ 3] %TEXT_RJ("기교+30%", 14)%　…　주는 쾌감 등이 30％ 증가
PRINTFORML  [ 4] %TEXT_RJ("정신력+30%", 14)%　…　받는 쾌감＆황홀 확률이 30％ 감소
PRINTFORML  [ 5] %TEXT_RJ("매력+30%", 14)%　…　상대에게 주는 불쾌감이 30％ 감소＆호감도＋１
PRINTFORML  [ 6] %TEXT_RJ("스톡증가", 14)% … 스톡을 １개 추가（중복 불가）
PRINTFORML  [10] %TEXT_RJ("Ｃ강화+50%", 14)%　…　상대에게 주는 Ｃ쾌감이 50％ 증가
PRINTFORML  [11] %TEXT_RJ("Ｖ강화+50%", 14)%　…　상대에게 주는 Ｖ쾌감이 50％ 증가
PRINTFORML  [12] %TEXT_RJ("Ａ강화+50%", 14)%　…　상대에게 주는 Ａ쾌감이 50％ 증가
PRINTFORML  [13] %TEXT_RJ("Ｂ강화+50%", 14)%　…　상대에게 주는 Ｂ쾌감이 50％ 증가
PRINTFORML  [14] %TEXT_RJ("Ｍ강화+50%", 14)%　…　상대에게 주는 Ｍ쾌감이 50％ 증가
PRINTFORML  [20] %TEXT_RJ("무드+3", 14)% … 무드 상승+３
PRINTFORML  [21] %TEXT_RJ("실행치+7", 14)% … 커맨드 실행치가 ７ 증가
PRINTFORML  [22] %TEXT_RJ("받는쾌감+20%", 14)%　…　받는 쾌감이 20％ 증가
PRINTFORML  [23] %TEXT_RJ("押し倒され+20%", 14)%　…　밀려넘어질 확률이 20％ 증가
PRINTFORML  [24] %TEXT_RJ("취득경험치+20%", 14)%　…　획득하는 경험치가 20％ 증가
PRINTFORML  [30] %TEXT_RJ("항상우르즈군", 14)% … 항상 우르즈군상태가 된다
PRINTFORML  [31] %TEXT_RJ("항상미니멈", 14)% … 항상 미니멈상태가 된다
PRINTFORML  [32] %TEXT_RJ("항상센시브", 14)% … 항상 센시브상태가 된다
PRINTFORML  [33] %TEXT_RJ("항상인라지", 14)% … 항상 인라지상태가 된다
PRINTFORML  [50] %TEXT_RJ("은밀+20%", 14)%　…　탐색에서 적이 출현할 확률이 20％ 감소
PRINTFORML  [51] %TEXT_RJ("보물상자출현율+20%", 14)%　…　탐색에서 보물상자를 발견할 확률이 20％ 증가
PRINTFORML  [52] %TEXT_RJ("문발견율+20%", 14)%　…　탐색에서 문을 발견할 학률이 20％ 증가

RESETCOLOR
SIF LOCAL == 0
	SETCOLOR DEF_COLOR("회색")

PRINTFORML  [99] 어빌리티를 한개 제거
PRINTFORML [100] 이걸로 좋다

RESETCOLOR
$INPUT_LOOP
INPUT
IF RESULT >= 1 && RESULT <= 100 && LOCAL:RESULT
	SELECTCASE RESULT
	CASE 100
		REDRAW 1
		RETURN 1
	CASE 99
		;一番下の効果を消す
		FLAG:(300 + ARG*10 + LOCAL) = 0
	CASE 1 TO 5
		FLAG:(300 + ARG*10 + LOCAL + 1) = RESULT*1000 + 30
	CASE 10 TO 14
		FLAG:(300 + ARG*10 + LOCAL + 1) = RESULT*1000 + 50
	CASE 20
		FLAG:(300 + ARG*10 + LOCAL + 1) = RESULT*1000 + 3
	CASE 21
		FLAG:(300 + ARG*10 + LOCAL + 1) = RESULT*1000 + 7
	CASE 22 TO 24, 50 TO 52
		FLAG:(300 + ARG*10 + LOCAL + 1) = RESULT*1000 + 20
	CASEELSE
		FLAG:(300 + ARG*10 + LOCAL + 1) = RESULT*1000
	ENDSELECT
ELSE
	GOTO INPUT_LOOP
ENDIF

CLEARLINE LINECOUNT - MEMO_LINECOUNT
RESTART
