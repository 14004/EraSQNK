;-------------------------------------------------
;조교커맨드実行前の파라미터の記録
;TCVAR:100～119を"조교커맨드実行前の重요정파라미터の記録"用として確保してある
;-------------------------------------------------
@SAVE_PALAM_BEFORECOM
#DIM LCOUNT
FOR LCOUNT, 1, CHARANUM
	;사정or절정게이지の開始値＆増加量
	TCVAR:LCOUNT:100 = BASE:LCOUNT:절정
	TCVAR:LCOUNT:101 = 0
	;모유게이지の開始値＆増加量
	TCVAR:LCOUNT:102 = BASE:LCOUNT:분유
	TCVAR:LCOUNT:103 = 0
	;촉수사정게이지の開始値＆増加量
	TCVAR:LCOUNT:104 = BASE:LCOUNT:촉수
	TCVAR:LCOUNT:105 = 0
	;드래곤사정게이지の開始値＆増加量
	TCVAR:LCOUNT:106 = BASE:LCOUNT:드래곤
	TCVAR:LCOUNT:107 = 0
	;정력の開始値＆減少量
	TCVAR:LCOUNT:108 = BASE:LCOUNT:정력
	TCVAR:LCOUNT:109 = 0
	;受精확률
	TCVAR:LCOUNT:110 = COND("受精확률", LCOUNT)
NEXT


;-------------------------------------------------
;PALAMの増減を表示させる（Ｐ윤は表示しない）
;対応する구슬の増加分も表示するように変更
;ARGにはTARGETかASSIかTARGET:1しか入らない
;-------------------------------------------------
@NEW_UPCHECK_PALAM, ARG
#DIM LCOUNT
#DIM MEMO_LINECOUNT
;このターンでの増減を行う前のPALAMの値の記録に使う
#DIM PALAM_PRE, 16
#DIM PALAM_NUM


;Ｗ넘어뜨리기の時には名前を表示させる
SIF TEQUIP:Ｗ넘어뜨리기 && SELECTCOM:1 >= 0
	PRINTFORML  ＜%CALLNAME:ARG%＞

PRINTL 

MEMO_LINECOUNT = LINECOUNT

FOR LCOUNT, 0, 16
	PALAM_PRE:LCOUNT = PALAM:ARG:LCOUNT
	PALAM:ARG:LCOUNT = MIN(PALAM_LV(16), PALAM:ARG:LCOUNT + CUP:ARG:LCOUNT - CDOWN:ARG:LCOUNT)
NEXT

FOR LCOUNT, 0, 16
	SELECTCASE LCOUNT
	CASE 0 TO 2
		PALAM_NUM = LCOUNT
	CASE 3
		PALAM_NUM = 14
	CASE 4
		PALAM_NUM = 15
	CASEELSE
		PALAM_NUM = LCOUNT - 2
	ENDSELECT

	SIF CUP:ARG:PALAM_NUM == 0 && CDOWN:ARG:PALAM_NUM == 0
		CONTINUE

	PRINTFORM  %TEXT_LJ(PALAMNAME:PALAM_NUM, 8)% = {PALAM_PRE:PALAM_NUM, 8} + 

	SELECTCASE GET_PALAMLV(CUP:ARG:PALAM_NUM)
	CASE 3
		SETCOLOR DEF_COLOR("황색")
	CASE IS >= 4
		SETCOLOR DEF_COLOR("핑크")
	ENDSELECT
	PRINTFORM {CUP:ARG:PALAM_NUM, 7}
	RESETCOLOR

	PRINTFORM  - {CDOWN:ARG:PALAM_NUM, 7} = {PALAM:ARG:PALAM_NUM, 8} 

	;宝구슬を獲得するようならそれを表示
	IF GET_JUEL(PALAM_NUM)
		PRINTFORM %PALAMNAME:PALAM_NUM%의 구슬＋
		SETCOLOR DEF_COLOR("황색")
		PRINTFORM {GET_JUEL(PALAM_NUM), 5}
	ENDIF

	RESETCOLOR
	PRINTL 
NEXT

;変動したPALAMを表示した場合には改行
SIF LINECOUNT != MEMO_LINECOUNT
	PRINTL 


;-------------------------------------------------
;表示を整形したUPCHECK
;-------------------------------------------------
@NEW_UPCHECK
#DIM MEMO_LINECOUNT
#DIM LCOUNT
#DIM BASE_PRE, 10
#DIM BASE_AFT, 10
#DIM UP_BASE, 10
#DIM DOWN_BASE, 10
#DIM BASE_MAX, 10
#DIMS NAME_BASE, 10

;改行をするかどうかに用いる
MEMO_LINECOUNT = LINECOUNT

CALL NEW_UPCHECK_PALAM, TARGET

SIF TEQUIP:Ｗ넘어뜨리기 && SELECTCOM:1 >= 0
	CALL NEW_UPCHECK_PALAM, TARGET:1

SIF LINECOUNT == MEMO_LINECOUNT
	PRINTL 

;PLAYERの페니스のＰ윤の処理
CALL SETFLAG, "Ｐ윤変動処理", MASTER
IF COND("３Ｐ")
	SWAP SELECTCOM, SELECTCOM:2
	CALL SETFLAG, "Ｐ윤変動処理", PLAYER:1
	SWAP SELECTCOM, SELECTCOM:2
ENDIF

VARSET BASE_PRE
VARSET BASE_AFT
VARSET UP_BASE
VARSET DOWN_BASE
VARSET BASE_MAX
VARSET NAME_BASE

;사정or절정
BASE_AFT:0 = BASE:MASTER:절정
BASE_PRE:0 = TCVAR:MASTER:100
UP_BASE:0 = MAX(TCVAR:MASTER:101, 0)
DOWN_BASE:0 = BASE_PRE:0 + UP_BASE:0 - BASE_AFT:0
BASE_MAX:0 = MAXBASE:MASTER:절정

NAME_BASE:0 = \@ PENIS(MASTER) ? 사정 # 절정 \@

;정력
BASE_AFT:1 = BASE:MASTER:정력
BASE_PRE:1 = TCVAR:MASTER:108
DOWN_BASE:1 = TCVAR:MASTER:109
;UP_BASE:1 = MAX(BASE_AFT:1 + UP_BASE:1 - BASE_PRE:1, 0)
UP_BASE:1 = MAX(BASE_AFT:1 + DOWN_BASE:1 - BASE_PRE:1, 0)
NAME_BASE:1 = 정력

;사정or절정（조수）
IF ASSI
	BASE_AFT:2 = BASE:ASSI:절정
	BASE_PRE:2 = TCVAR:ASSI:100
	UP_BASE:2 = MAX(TCVAR:ASSI:101, 0)
	DOWN_BASE:2 = BASE_PRE:2 + UP_BASE:2 - BASE_AFT:2
	BASE_MAX:2 = MAXBASE:ASSI:절정
	NAME_BASE:2 = \@ PENIS(ASSI) ? 사정 # 절정 \@

	;정력（조수）
	BASE_AFT:3 = BASE:ASSI:정력
	BASE_PRE:3 = TCVAR:ASSI:108
	DOWN_BASE:3 = TCVAR:ASSI:109
	UP_BASE:3 = MAX(BASE_AFT:3 + DOWN_BASE:3 - BASE_PRE:3, 0)
	NAME_BASE:3 = 정력(助)
ENDIF

;사정（조교대상）
IF PENIS(TARGET)
	BASE_AFT:4 = BASE:절정
	BASE_PRE:4 = TCVAR:100
	UP_BASE:4 = MAX(TCVAR:101, 0)
	DOWN_BASE:4 = BASE_PRE:4 + UP_BASE:4 - BASE_AFT:4
	BASE_MAX:4 = MAXBASE:절정
	NAME_BASE:4 = 사정(調)
ENDIF

;정력（조교대상）
BASE_AFT:5 = BASE:정력
BASE_PRE:5 = TCVAR:108
DOWN_BASE:5 = TCVAR:109
UP_BASE:5 = MAX(BASE_AFT:5 + DOWN_BASE:5 - BASE_PRE:5, 0)
NAME_BASE:5 = 정력(調)

;모유（조교대상）
IF TALENT:모유체질
	BASE_AFT:6 = BASE:분유
	BASE_PRE:6 = TCVAR:102
	UP_BASE:6 = MAX(TCVAR:103, 0)
	DOWN_BASE:6 = BASE_PRE:6 + UP_BASE:6 - BASE_AFT:6
	BASE_MAX:6 = MAXBASE:분유
	NAME_BASE:6 = 모유(조교대상)
ENDIF
;사정（촉수）
IF TEQUIP:촉수
	BASE_AFT:7 = BASE:MASTER:촉수
	BASE_PRE:7 = TCVAR:MASTER:104
	DOWN_BASE:7 = TFLAG:15*MAXBASE:MASTER:촉수
	UP_BASE:7 = MAX(BASE_AFT:7 + DOWN_BASE:7 - BASE_PRE:7, 0)
	NAME_BASE:7 = 사정(触)
ENDIF
;사정（드래곤）
IF SELECTCOM == 304 || SELECTCOM == 305
	BASE_AFT:8 = BASE:MASTER:드래곤
	BASE_PRE:8 = TCVAR:MASTER:106
	DOWN_BASE:8 = TFLAG:9 * MAXBASE:MASTER:드래곤
	UP_BASE:8 = MAX(BASE_AFT:8 + DOWN_BASE:8 - BASE_PRE:8, 0)
	NAME_BASE:8 = 사정(ド)
ENDIF

MEMO_LINECOUNT = LINECOUNT

FOR LCOUNT, 0, 9
	SIF NAME_BASE:LCOUNT == ""
		CONTINUE
	SIF UP_BASE:LCOUNT == 0 && DOWN_BASE:LCOUNT == 0
		CONTINUE

	PRINTFORM  %TEXT_LJ(NAME_BASE:LCOUNT, 8)% = {BASE_PRE:LCOUNT, 8} + 

	;사정の時などは色を変える
	IF BASE_MAX:LCOUNT
		SELECTCASE UP_BASE:LCOUNT
		CASE IS >= BASE_MAX:LCOUNT*4/5
			SETCOLOR DEF_COLOR("핑크")
		CASE IS >= BASE_MAX:LCOUNT/4
			SETCOLOR DEF_COLOR("황색")
		ENDSELECT
	ENDIF

	PRINTFORM {UP_BASE:LCOUNT, 7}

	RESETCOLOR

	PRINTFORML  - {DOWN_BASE:LCOUNT, 7} = {BASE_AFT:LCOUNT, 8} %NAME_BASE:LCOUNT%
NEXT

SIF LINECOUNT == MEMO_LINECOUNT
	CLEARLINE 1

CFLAG:490 = 0

IF BASE:PLAYER:정력 <= 0 && ASSIPLAY && STATE("撃沈", ASSI)
	PRINTL  
	PRINTFORMW ★조교자를 %조사처리(CALLNAME:MASTER,"로")% 교대했습니다. ★
	BASE:ASSI:기력 = 0
	TEQUIP:넘어뜨리기 = 0
	SIF TARGET:1 == ASSI
		CALL SETFLAG, "Ｗ넘어뜨리기解除"
	CALL REMOVE_STATE, ASSI, "황홀"
	ASSIPLAY = 0
	PLAYER = MASTER
ENDIF
